apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'application'

sourceCompatibility = 1.7
version = '0.0.1'

ext.platform = "windows"
ext.dropboxPath = "D:/Dropbox/Dropbox"

mainClassName = "org.cjforge.hexed.GameCore"
applicationDefaultJvmArgs = ["-Djava.library.path=../lib/"]
applicationDistribution.from("${buildDir}/natives/${platform}") {
    into "lib"
}

repositories {
    mavenCentral()
}



dependencies {
    testCompile group: 'junit', name: 'junit', version: '4.11'
    compile 'org.slick2d:slick2d-core:1.0.0'
    compile 'log4j:log4j:1.2.17'
}

//compileJava.dependsOn "natives"

platforms = ['windows', 'linux', 'osx']
platforms.each { platform ->
    task "${platform}Natives" {
        outputDir = "$buildDir/natives/$platform"
        inputs.files(configurations.compile)
        outputs.dir(outputDir)
        doLast {
            copy {
                def artifacts = configurations.compile.resolvedConfiguration.resolvedArtifacts
                        .findAll { it.classifier == "natives-$platform" }
                artifacts.each {
                    from zipTree(it.file)
                }
                into outputDir
            }
        }
    }
}
task natives {
    description "Copies native libraries to an appropriate directory."
    dependsOn platforms.collect { "${it}Natives" }.findAll { tasks[it] }
}

distZip{
    dependsOn natives
}

task deployToDropbox(type: Copy, dependsOn: distZip){
	from("${buildDir}/distributions"){
		include distZip.archiveName
	}
	into "${dropboxPath}/releases"
}




